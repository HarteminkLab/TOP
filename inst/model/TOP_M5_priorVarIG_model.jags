model {
  for(i in 1:N) {
    chip[i] ~ dnorm(  mu[i],  tau[tf[i], cell_type[i]] )

    mu[i] <- alpha[tf[i], cell_type[i]] +
      beta1[tf[i], cell_type[i]] * pwm[i] +
      beta2[tf[i], cell_type[i]] * bin1[i] +
      beta3[tf[i], cell_type[i]] * bin2[i] +
      beta4[tf[i], cell_type[i]] * bin3[i] +
      beta5[tf[i], cell_type[i]] * bin4[i] +
      beta6[tf[i], cell_type[i]] * bin5[i]
  }

  # bottom level
  for(j in 1:n_tfs) {
    for(k in 1:n_cell_types) {
      alpha[j, k] ~ dnorm(Alpha[j], tau_alpha)
      beta1[j, k] ~ dnorm(Beta1[j], tau_beta1)
      beta2[j, k] ~ dnorm(Beta2[j], tau_beta2)
      beta3[j, k] ~ dnorm(Beta3[j], tau_beta3)
      beta4[j, k] ~ dnorm(Beta4[j], tau_beta4)
      beta5[j, k] ~ dnorm(Beta5[j], tau_beta5)
      beta6[j, k] ~ dnorm(Beta6[j], tau_beta6)
      # In dgamma(r, lambda), mean = r / lambda, variance = r / lambda^2.
      # use a common mean of Tau, and variance of 1, so r = Tau^2 and lambda = Tau
      tau[j, k]   ~ dgamma(Tau[j]^2, Tau[j])
    }
  }

  tau_alpha ~ dgamma(1, 1)
  tau_beta1 ~ dgamma(1, 1)
  tau_beta2 ~ dgamma(1, 1)
  tau_beta3 ~ dgamma(1, 1)
  tau_beta4 ~ dgamma(1, 1)
  tau_beta5 ~ dgamma(1, 1)
  tau_beta6 ~ dgamma(1, 1)

  # middle level
  for(j in 1:n_tfs) {
    Alpha[j] ~ dnorm(A, tau_Alpha)
    Beta1[j] ~ dnorm(B1, tau_Beta1)
    Beta2[j] ~ dnorm(B2, tau_Beta2)
    Beta3[j] ~ dnorm(B3, tau_Beta3)
    Beta4[j] ~ dnorm(B4, tau_Beta4)
    Beta5[j] ~ dnorm(B5, tau_Beta5)
    Beta6[j] ~ dnorm(B6, tau_Beta6)
    Tau[j]   ~ dgamma(T^2, T)
  }
  tau_Alpha ~ dgamma(1, 1)
  tau_Beta1 ~ dgamma(1, 1)
  tau_Beta2 ~ dgamma(1, 1)
  tau_Beta3 ~ dgamma(1, 1)
  tau_Beta4 ~ dgamma(1, 1)
  tau_Beta5 ~ dgamma(1, 1)
  tau_Beta6 ~ dgamma(1, 1)

  # top level
  A ~ dnorm(0, 1)
  B1 ~ dnorm(0, 1)
  B2 ~ dnorm(0, 1)
  B3 ~ dnorm(0, 1)
  B4 ~ dnorm(0, 1)
  B5 ~ dnorm(0, 1)
  B6 ~ dnorm(0, 1)
  T  ~ dgamma(1, 1)

}
